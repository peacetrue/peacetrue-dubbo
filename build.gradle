plugins {
    id("java-library")
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id("maven-publish")
    id("signing")
    id("idea")
}

allprojects { project ->

    group "com.github.peacetrue"
    version "2.0.0-SNAPSHOT"
    description "提供 dubbo 扩展支持"

    apply plugin: "java"
    apply plugin: "java-library"
    sourceCompatibility = 8
    targetCompatibility = 8

    apply plugin: "idea"
    idea {
        module {
            inheritOutputDirs = false
            outputDir = tasks.compileJava.destinationDir
            testOutputDir = tasks.compileTestJava.destinationDir
            downloadSources = false
            downloadJavadoc = false
        }
    }

    repositories {
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        mavenCentral()
        jcenter()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:2.1.8.RELEASE"
//            mavenBom "com.github.peacetrue:peacetrue-dependencies:2.0.0-SNAPSHOT"
//            mavenBom "org.apache.dubbo:dubbo-dependencies-bom:2.7.3"
        }

        dependencies {
            dependency("org.apache.dubbo:dubbo:2.7.3")
            dependency("com.google.code.findbugs:jsr305:3.0.2")
            dependency("org.projectlombok:lombok:1.18.6")
        }
    }

    ext {
        revision = "2.7.3"
        dubbo = [version: revision]
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
        api "org.slf4j:slf4j-api"
        implementation "com.google.code.findbugs:jsr305"
        implementation("org.apache.dubbo:dubbo") {
            exclude group: "log4j", module: "log4j"
            exclude group: "javax.servlet", module: "servlet-api"
            exclude group: "org.springframework", module: "spring-context"
        }

        testImplementation "junit:junit"
        testCompileOnly "org.projectlombok:lombok"
        testAnnotationProcessor "org.projectlombok:lombok"
        testImplementation "ch.qos.logback:logback-classic"
        testImplementation "org.springframework.boot:spring-boot-starter"
        testImplementation "org.apache.dubbo:dubbo-spring-boot-starter:${revision}"
        testImplementation("org.apache.dubbo:dubbo-dependencies-zookeeper:${dubbo.version}") {
            exclude group: "log4j", module: "log4j"
            exclude group: "org.slf4j", module: "slf4j-log4j12"
        }
//        testImplementation('org.apache.dubbo:dubbo-spring-boot-starter:2.7.3') {
//            exclude group: "log4j", module: "log4j"
//            exclude group: "javax.servlet", module: "servlet-api"
//            exclude group: "org.springframework", module: "spring-context"
//        }
//        testImplementation('org.apache.dubbo:dubbo-dependencies-zookeeper:2.7.3') {
//            exclude group: "org.slf4j", module: "slf4j-log4j12"
//        }
//        testImplementation 'org.springframework.boot:spring-boot-starter-test'
//        testImplementation 'org.springframework.boot:spring-boot-starter-actuator'

    }

    task antoraCopySourceToExample() {
        doLast {
            def path = "${rootDir.path}/docs/antora/modules/ROOT/examples/${project.name}"
            delete {
                delete(path)
            }
            copy {
                from sourceSets.main.allSource into(path)
            }
            copy {
                from sourceSets.test.allSource into(path)
            }
        }
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = "sources"
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        archiveClassifier = "javadoc"
        from javadoc.destinationDir
    }

    def repository_snapshots = "https://oss.sonatype.org/content/repositories/snapshots/"
    def repository_releases = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

    apply plugin: "maven-publish"
    apply plugin: "signing"
    publishing {
        publications {
            mavenJava(MavenPublication) {
                pom {
                    name = project.name
                    description = project.description
                    url = "https://github.com/peacetrue/${project.name}"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "xiayouxue"
                            name = "xiayouxue"
                            email = "xiayouxue@hotmail.com"
                        }
                    }
                    scm {
                        connection = "https://github.com/peacetrue/${project.name}.git"
                        developerConnection = "https://github.com/peacetrue/${project.name}.git"
                        url = "https://github.com/peacetrue/peacetrue/${project.name}"
                    }
                }

                from components.java

                artifact sourcesJar
                artifact javadocJar
            }
        }

        repositories {
            maven {
                url version.endsWith("SNAPSHOT") ? repository_snapshots : repository_releases
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }

}
